
message(STATUS "Compiler ID : " ${CMAKE_CXX_COMPILER_ID})
message(STATUS "CMAKE_BUILD_TYPE : " ${CMAKE_BUILD_TYPE})

add_library(lexer-include-source INTERFACE)
target_include_directories(lexer-include-source INTERFACE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>)
add_library(lexer-source INTERFACE)
target_sources(lexer-source INTERFACE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>/lexer.cpp)
target_link_libraries(lexer-source INTERFACE lexer-include-source)
add_library(lexer lexer.cpp)
target_include_directories(lexer PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> )
target_include_directories(lexer PUBLIC "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>")

if(MSVC)
  if("${MSVC_TOOLSET_VERSION}" STREQUAL "140")
    target_compile_options(lexer INTERFACE /W0 /sdl)
    set(LEXER_LEGACY_VISUAL_STUDIO TRUE)
  else()
    target_compile_options(lexer INTERFACE /WX /W3 /sdl /w34714) # https://docs.microsoft.com/en-us/cpp/error-messages/compiler-warnings/compiler-warning-level-4-c4714?view=vs-2019
  endif()
else(MSVC)
  if(NOT WIN32)
    target_compile_options(lexer INTERFACE -fPIC)
  endif()
  message(STATUS "Assuming GCC-like compiler.")
  target_compile_options(lexer PRIVATE -Wall -Wextra -Weffc++)
  if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
    target_compile_options(lexer PRIVATE -Wsuggest-override)
  endif()
  if((CMAKE_CXX_COMPILER_ID STREQUAL "Clang") OR (CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang"))
    target_compile_options(lexer PRIVATE -Winconsistent-missing-override)
  endif()
  target_compile_options(lexer PRIVATE -Wfatal-errors -Wsign-compare -Wshadow -Wwrite-strings -Wpointer-arith -Winit-self -Wconversion -Wno-sign-conversion)
endif(MSVC)

# workaround for GNU GCC poor AVX load/store code generation
if ((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") AND (CMAKE_SYSTEM_PROCESSOR MATCHES "^(i.86|x86(_64)?)$"))
  target_compile_options(lexer PRIVATE -mno-avx256-split-unaligned-load -mno-avx256-split-unaligned-store)
endif()
if(LEXER_DEVELOPMENT_CHECKS)
  target_compile_definitions(lexer PUBLIC LEXER_DEVELOPMENT_CHECKS=1)
endif()
if((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") AND (CMAKE_BUILD_TYPE STREQUAL "Debug"))
  message(STATUS "Enabling _GLIBCXX_DEBUG")
  target_compile_definitions(lexer PRIVATE _GLIBCXX_DEBUG=1)
endif()

if(LEXER_SANITIZE)
  target_compile_options(lexer INTERFACE -fsanitize=address  -fno-omit-frame-pointer -fno-sanitize-recover=all)
  target_compile_definitions(lexer INTERFACE ASAN_OPTIONS=detect_leaks=1)
  target_link_libraries(lexer INTERFACE -fsanitize=address  -fno-omit-frame-pointer -fno-sanitize-recover=all)
endif()

if(LEXER_LOGGING)
  target_compile_definitions(lexer PRIVATE LEXER_LOGGING=1)
endif()
