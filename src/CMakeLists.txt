
message(STATUS "Compiler ID : " ${CMAKE_CXX_COMPILER_ID})
message(STATUS "CMAKE_BUILD_TYPE : " ${CMAKE_BUILD_TYPE})

add_library(merve-include-source INTERFACE)
target_include_directories(merve-include-source INTERFACE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>)
add_library(merve-source INTERFACE)
target_sources(merve-source INTERFACE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>/parser.cpp)
target_link_libraries(merve-source INTERFACE merve-include-source)
add_library(merve parser.cpp)
target_include_directories(merve PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> )
target_include_directories(merve PUBLIC "$<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>")

if(NOT DEFINED CMAKE_POSITION_INDEPENDENT_CODE)
  # We default to ON for all targets, so that we can use the library in shared libraries.
  set_target_properties(merve PROPERTIES POSITION_INDEPENDENT_CODE ON)
endif(NOT DEFINED CMAKE_POSITION_INDEPENDENT_CODE)
if(MSVC)
  if("${MSVC_TOOLSET_VERSION}" STREQUAL "140")
    target_compile_options(merve INTERFACE /W0 /sdl)
    set(MERVE_LEGACY_VISUAL_STUDIO TRUE)
  else()
    target_compile_options(merve PRIVATE /WX /W3 /sdl /w34714) # https://docs.microsoft.com/en-us/cpp/error-messages/compiler-warnings/compiler-warning-level-4-c4714?view=vs-2019
  endif()
else(MSVC)
  message(STATUS "Assuming GCC-like compiler.")
  target_compile_options(merve PRIVATE -Wall -Wextra -Weffc++)
  if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
    target_compile_options(merve PRIVATE -Wsuggest-override)
  endif()
  if((CMAKE_CXX_COMPILER_ID STREQUAL "Clang") OR (CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang"))
    target_compile_options(merve PRIVATE -Winconsistent-missing-override)
  endif()
  target_compile_options(merve PRIVATE -Wfatal-errors -Wsign-compare -Wshadow -Wwrite-strings -Wpointer-arith -Winit-self -Wconversion -Wno-sign-conversion)
endif(MSVC)

# workaround for GNU GCC poor AVX load/store code generation
if ((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") AND (CMAKE_SYSTEM_PROCESSOR MATCHES "^(i.86|x86(_64)?)$"))
  target_compile_options(merve PRIVATE -mno-avx256-split-unaligned-load -mno-avx256-split-unaligned-store)
endif()
if(MERVE_DEVELOPMENT_CHECKS)
  target_compile_definitions(merve PUBLIC MERVE_DEVELOPMENT_CHECKS=1)
endif()
if((CMAKE_CXX_COMPILER_ID STREQUAL "GNU") AND (CMAKE_BUILD_TYPE STREQUAL "Debug"))
  message(STATUS "Enabling _GLIBCXX_DEBUG")
  target_compile_definitions(merve PUBLIC _GLIBCXX_DEBUG=1)
endif()

if(MERVE_SANITIZE)
  target_compile_options(merve PUBLIC -fsanitize=address  -fno-omit-frame-pointer -fno-sanitize-recover=all)
  target_compile_definitions(merve PUBLIC ASAN_OPTIONS=detect_leaks=1)
  target_link_libraries(merve PUBLIC -fsanitize=address  -fno-omit-frame-pointer -fno-sanitize-recover=all)
endif()

if(MERVE_LOGGING)
  target_compile_definitions(merve PRIVATE MERVE_LOGGING=1)
endif()

if(MERVE_USE_SIMDUTF)
  target_link_libraries(merve PRIVATE simdutf)
  target_compile_definitions(merve PRIVATE MERVE_USE_SIMDUTF=1)
endif()
